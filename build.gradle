import org.springframework.boot.gradle.plugin.SpringBootPlugin

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'org.springframework.boot:spring-boot-gradle-plugin:2.6.4'
    }
}

ext {
    springCloudVersion = "2021.0.1"
    alibabaCloudVersion = "2021.1"
}

allprojects {
    repositories {
        mavenCentral()
    }
}

subprojects {
    apply plugin: "java"
    apply plugin: "org.springframework.boot"
    apply plugin: "io.spring.dependency-management"

    version = "0.1.0"

    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'

    def defaultEncoding = 'UTF-8'
    tasks.withType(AbstractCompile).forEach { it.options.encoding = defaultEncoding }

    javadoc {
        options.encoding = defaultEncoding
        options.addBooleanOption('Xdoclint:none', true)
    }

    compileJava.dependsOn(processResources)

    springBoot {
        buildInfo()
    }

    bootJar {
        archiveFileName.set('app.jar')
    }

    dependencyManagement {
        imports {
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
            mavenBom "com.alibaba.cloud:spring-cloud-alibaba-dependencies:${alibabaCloudVersion}"
            mavenBom SpringBootPlugin.BOM_COORDINATES
        }
    }

    dependencies {
        annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"
        compileOnly "org.springframework.boot:spring-boot-configuration-processor"
        implementation "org.springframework.boot:spring-boot-starter-actuator"
        implementation "org.springframework.cloud:spring-cloud-starter"
        implementation "org.springframework.cloud:spring-cloud-starter-bootstrap"
        implementation "com.alibaba.cloud:spring-cloud-starter-alibaba-nacos-config"
        implementation "com.alibaba.cloud:spring-cloud-starter-alibaba-nacos-discovery"
        implementation fileTree(dir: 'libs', includes: ['*.jar'])
    }
}